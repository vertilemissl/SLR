<S>→<程序>
<程序>→<分程序>.
<分程序>→ <常量说明部分><变量说明部分><过程说明部分><语句>
<常量说明部分>→ε
<常量说明部分> → const <常量定义><常量定义组>;
<常量定义组>→,<常量定义><常量定义组>
<常量定义组>→ε
<常量定义> → $id = $uni
<变量说明部分> → var <参数组>; 
<变量说明部分>→ε
<参数组>→ $id <标识符组>
<标识符组>→, $id <标识符组>
<标识符组>→ε
<过程说明部分>→ε
<过程说明部分> → <过程首部><分程序><过程说明部分>
<过程首部> → procedure $id;
<语句> → <赋值语句>
<语句> → <条件语句>
<语句> → <当型循环语句>
<语句> → <过程调用语句>
<语句> → <读语句>
<语句> → <写语句>
<语句> → <复合语句>
<语句> →ε
<赋值语句> → $id := <表达式>
<条件语句> → if<条件>then<语句>
<条件> → <表达式>$ro<表达式>
<条件> → odd<表达式>
<表达式> → <项><表达式组>
<表达式组> → $pm<项><表达式组> 
<表达式组> →ε
<项>→<因子><因子组>
<因子组> → $md<因子><因子组>
<因子组>→ε
<因子> → $id
<因子> → $uni
<因子> →(<表达式>)
<读语句> → read(<参数组>)
<写语句> → write(<参数组>)
<复合语句> → begin<语句><中间语句>end
<中间语句>→;<语句><中间语句>
<中间语句>→ε
<当型循环语句> → while<条件>do<语句>
<过程调用语句> → call $id